apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply plugin: 'com.github.dcendents.android-maven'

group 'com.percolate.sdk'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    buildTypes {
        debug {
            testCoverageEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
    }
}

repositories {
    mavenCentral()
}

task coverage(type:JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = 'Create code coverage report for android module'

    classDirectories = fileTree(
            dir: "$buildDir/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )

    sourceDirectories = files("src/main/java")
    executionData = files("build/jacoco/testDebugUnitTest.exec")

    reports {
        //noinspection GroovyAssignabilityCheck
        xml {
            enabled true
            destination file("build/jacocoAndroidTests.xml")
        }
        html.enabled = true
    }
}

dependencies {
    api project(':api')

    // JUnit
    testImplementation 'junit:junit:4.12'

    // OpenPojo
    testImplementation 'com.openpojo:openpojo:0.8.3'

    // Kotlin Standard Library
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Robolectric
    testImplementation 'org.robolectric:robolectric:3.0'
}

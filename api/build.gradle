apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'java-library'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'com.percolate.sdk'

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task generateSources() {
    def outputDir = file("$buildDir/../src/main/java/com/percolate/sdk/api/utils")
    outputs.dir outputDir
    doFirst {
        outputDir.exists() || outputDir.mkdirs()
        def srcFile = new File(outputDir, "ProjectVersion.java")
        srcFile.write(
                """package com.percolate.sdk.api.utils;

  public class ProjectVersion {
      public static final String VERSION = "$project.version";
}
""")
    }
}
compileJava.dependsOn generateSources
compileJava.source generateSources.outputs.files, sourceSets.main.java

task skipWarningsJavaDoc(type: Javadoc) {
    options.addStringOption("Xdoclint:none")
}

task javadocJar(type: Jar, dependsOn: skipWarningsJavaDoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Javadoc) {
    options.addStringOption('-Xdoclint:none')
}

task coverage(dependsOn: jacocoTestReport) {
    group = "Reporting"
    description = 'Create code coverage report for api module'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/jacocoApiTests.xml")
    }
}

dependencies {
    api project(':core')

    // Base64 Encoder/Decoder
    api 'net.iharder:base64:2.3.9'

    // Guava
    api 'com.google.guava:guava:24.1-android'

    // Retrofit
    api "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    api "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"
    api "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"

    //Android http client
    api 'com.squareup.okhttp3:okhttp:3.10.0'

    testImplementation 'junit:junit:4.12'

    // Kotlin Standard Library
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

}
